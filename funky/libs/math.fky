# standard math functions in funky
module math where

    import "stdlib.fky"

    # mathematical constants
    e   = 2.718281828459045
    pi  = 3.141592653589793
    tau = 2 * pi

    abs n given n >= 0    = n
          given otherwise = -n
    
    signum n given n > 0     = 1
             given n < 0     = -1
             given otherwise = 0

    factorial 0              = 1
    factorial n given n >= 0 = n * factorial (n - 1)

    gcd a 0 = a
    gcd a b = gcd b (a % b)

    lcm a b = (a / g) * b
              where g = gcd a b

    coprime x y = gcd x y == 1

    recip x = 1.0 / x

    exp x = e ** x

    sqrt = flip (**) 0.5

    even n = n % 2 == 0
    odd    = not `compose` even

    max a b given a >= b    = a
            given otherwise = b

    min a b given a <= b    = a
            given otherwise = b
