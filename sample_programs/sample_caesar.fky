# Implementation of the Caesar cipher in Funky.
module caesar with

    import "stdlib.fky"
    import "random.fky"
    import "string.fky"

    newtype Maybe = Just Integer | Nothing

    alphabet = "abcdefghijklmnopqrstuvwxyz"

    index c ""                 = Nothing
    index c st given c == h    = Just 0
               given otherwise = match index c t on
                                    Nothing -> Nothing
                                    Just x  -> Just (x + 1)
               with
                h = str_head st
                t = str_tail st

    encode n "" = ""
    encode n st = encoded ++ encode n t
                  with h = str_head st
                       t = str_tail st
                       encoded = match index h alphabet on
                                   Nothing -> h
                                   Just i  -> char_at
                                              ((i + n) % (strlen alphabet))
                                              alphabet

    decode n = encode (-n)

    rot13 = encode 13

    main = encode key "this is a *secret message*"
           with key = rand 51773
